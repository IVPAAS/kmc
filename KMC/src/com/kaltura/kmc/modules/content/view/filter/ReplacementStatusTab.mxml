<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%"
		 implements="com.kaltura.kmc.modules.content.view.filter.IFilterTab">
	<mx:Metadata>
		[Event(name="filterChanged", type="flash.events.Event")] 
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.kaltura.types.KalturaEntryReplacementStatus;
			import com.kaltura.vo.KalturaMediaEntryFilter;
			
			
			public function setFilter(filterVo:KalturaMediaEntryFilter):void {
				if (!filterVo.statusIn) {
					allReplacementStatusBtn.selected = true;
					noRequestBtn.selected = false;
					processingNewBtn.selected = false;
					readyForReviewBtn.selected = false;
				}
				else {
					var replacementStatusArr:Array = filterVo.replacementStatusIn.split(',');
					for each (var replaceCode:String in replacementStatusArr) {
						switch (replaceCode) {
							case KalturaEntryReplacementStatus.APPROVED_BUT_NOT_READY:
								noRequestBtn.selected = true;
								break;
							case KalturaEntryReplacementStatus.NOT_READY_AND_NOT_APPROVED:
								processingNewBtn.selected = true;
								break;
							case KalturaEntryReplacementStatus.READY_BUT_NOT_APPROVED:
								readyForReviewBtn.selected = true;
								break;
						}
					}
				}
			}
			
			/**
			 * filter by replacement status
			 * */
			public function getFilterString():String {
				var replacementStatusArray:Array = [];
				
				if (allReplacementStatusBtn.selected) {
					replacementStatusArray.push(KalturaEntryReplacementStatus.APPROVED_BUT_NOT_READY,
						KalturaEntryReplacementStatus.NOT_READY_AND_NOT_APPROVED, KalturaEntryReplacementStatus.READY_BUT_NOT_APPROVED);
				}
				else {
					if (noRequestBtn.selected) { // for no request
						replacementStatusArray.push(KalturaEntryReplacementStatus.APPROVED_BUT_NOT_READY);
					}
					if (processingNewBtn.selected) { // for processing new files 
						replacementStatusArray.push(KalturaEntryReplacementStatus.NOT_READY_AND_NOT_APPROVED);
					}
					if (readyForReviewBtn.selected) { // for ready for review
						replacementStatusArray.push(KalturaEntryReplacementStatus.READY_BUT_NOT_APPROVED);
					}
					
				}
				return replacementStatusArray.join(',');
			}
			
			/**
			 * user select to choose all replacement status
			 */
			private function onAllReplacementStatusClicked(evt:Event):void {
				var isSelected:Boolean = evt.target.selected;
				if (!isSelected) {
					(evt.target as Button).selected = true;
					noRequestBtn.selected = false;
					processingNewBtn.selected = false;
					readyForReviewBtn.selected = false;
					dispatchEvent(new Event(BaseFilter.FILTER_CHANGED));
				}
			}
			
			
			/**
			 * one of the replacement
			 * status buttons was clicked
			 */
			private function changeReplacementStatusFilter(evt:Event):void {
				//if a moderation button was selected deselect the all moderation status 
				if ((evt.target as Button).selected) {
					allReplacementStatusBtn.selected = false;
				}
				//if non is selected - highlight the 'allModeration' button
				if (validateReplacementStatus()) {
					allReplacementStatusBtn.selected = true;
				}
				dispatchEvent(new Event(BaseFilter.FILTER_CHANGED));
			}
			
			
			/**
			 * check if all replacement status buttons are selected - if non is selected return true
			 * if one of them is selected - return false
			 */
			private function validateReplacementStatus():Boolean {
				return !noRequestBtn.selected && !processingNewBtn.selected && !readyForReviewBtn.selected;
			}
			
			
			
			
			public function get buttons():Array {
				return [allReplacementStatusBtn, noRequestBtn, processingNewBtn, readyForReviewBtn];
			}
		]]>
	</mx:Script>
	<mx:Button id="allReplacementStatusBtn" width="100%" selected="true"
			   label="{resourceManager.getString('cms', 'allReplacementStatus')}" buttonMode="true"
			   click="{onAllReplacementStatusClicked(event)}" styleName="mainFilterGroupButton"/>
	<mx:Button id="noRequestBtn" width="100%" selected="false" label="{resourceManager.getString('cms', 'noRequestBtn')}"
			   buttonMode="true" toggle="true" styleName="innerFilterGroupButton"
			   click="{changeReplacementStatusFilter(event)}"/>
	<mx:Button id="processingNewBtn" width="100%" selected="false" label="{resourceManager.getString('cms', 'processingNewBtn')}"
			   buttonMode="true" toggle="true" styleName="innerFilterGroupButton"
			   click="{changeReplacementStatusFilter(event)}"/>
	<mx:Button id="readyForReviewBtn" width="100%" selected="false"
			   label="{resourceManager.getString('cms', 'readyForReviewBtn')}" buttonMode="true" toggle="true"
			   styleName="innerFilterGroupButton" click="{changeReplacementStatusFilter(event)}"/>

</mx:VBox>
