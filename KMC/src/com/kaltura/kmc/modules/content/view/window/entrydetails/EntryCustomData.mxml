<?xml version="1.0" encoding="utf-8"?>
<!---
	custom metadata tab of EntryDetailsWin
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="onCreationComplete(event)" show="onShow(event)" xmlns:itemrenderers="com.kaltura.kmc.modules.content.view.itemrenderers.*">
<mx:Script>
	<![CDATA[
		import com.kaltura.commands.uiConf.UiConfAdd;
		import com.kaltura.kmc.modules.content.events.MetadataDataEvent;
		import com.kaltura.kmc.modules.content.events.MetadataProfileEvent;
		import com.kaltura.kmc.modules.content.model.EntryDetailsModel;
		import com.kaltura.kmc.modules.content.model.FilterModel;
		import com.kaltura.kmc.modules.content.utils.FormBuilder;
		import com.kaltura.kmc.modules.content.vo.EntryMetadataDataVO;
		import com.kaltura.types.KalturaMetadataProfileStatus;
		import com.kaltura.vo.KMCMetadataProfileVO;
		
		import mx.binding.utils.BindingUtils;
		import mx.controls.Alert;
		import mx.core.Application;
		import mx.core.Container;
		import mx.core.UIComponent;

		private var _pending:Boolean = false;
		private var _form:UIComponent ;
		private var _isReady:Boolean = false;
		
		private var _finalMxml:XML;
		private var _formBuilder:FormBuilder;
		private var _isFormReady:Boolean = false;

		/**
		 * The form builder for current custom data tab
		 * */
		public function get formBuilder():FormBuilder
		{
			return _formBuilder;
		}

		public function set formBuilder(value:FormBuilder):void
		{
			_formBuilder = value;
			if (!_isFormReady && _isReady)
				checkProfile();
			_isFormReady = true;
			if (_finalMxml) 
				buildForm();
		}

		private function onCreationComplete(event:Event):void {
			if (_isFormReady)
				checkProfile();
			_isReady= true;
			
		
		}
		
		/**
		 * once the window is ready and all relvent data is ready we should check for the
		 * state of the profile
		 * */
		private function checkProfile():void {
			if (!_formBuilder.metadataProfile || !_formBuilder.metadataProfile.profile)
				return;
			_formBuilder.metadataInfo = new EntryMetadataDataVO();
			if (_formBuilder.metadataProfile && _formBuilder.metadataProfile.profile &&
				(_formBuilder.metadataProfile.profile.status == KalturaMetadataProfileStatus.TRANSFORMING)) {
				//if current profile is transforming, will request it again from the server, to see if ready
				var getMetadataProfile:MetadataProfileEvent = new MetadataProfileEvent(MetadataProfileEvent.GET, _formBuilder.metadataProfile.profile.id);
				getMetadataProfile.dispatch();
				_pending = true;
			}
			else {
				BindingUtils.bindSetter(addForm, _formBuilder,["metadataInfo","finalViewMxml"]);
				BindingUtils.bindSetter(disableComponentes, this.all, "enabled");
			}
		}
		
		
		/**
		 * not building a form if metadataProfile is pending
		 * */
		private function onShow(event:Event):void {
			if (_pending) {
				Alert.show(resourceManager.getString('cms', 'metadataPending'), resourceManager.getString('cms', 'metadataPendingTitle'));
			}
		}
		
		/**
		 * will be called when the final mxml is ready
		 * */
		private function addForm(finalMxml:XML) : void{
			if (finalMxml) {
				_finalMxml = finalMxml;
				if (_isFormReady) 
					buildForm();
			}
		}
		
		/**
		 * once the formBuilder and the _finalmxml are available we can build the form
		 * */
		private function buildForm():void {
			_form = formBuilder.buildLayout(_finalMxml);
			all.addChild(_form);
			if (_form && !all.enabled) {
				formBuilder.disableComponents(_form as Container);
			}
		}
		
		/**
		 * if the inner components should be disabled - from roles&Permissions
		 * */
		private function disableComponentes(value:Boolean) : void{
			if (_form && !value) {
				formBuilder.disableComponents(_form as Container);
			}
		}

	]]>
</mx:Script>
	<mx:VBox id="all" width="100%" disabledOverlayAlpha="0"/>
</mx:VBox>
