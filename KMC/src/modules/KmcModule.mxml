<?xml version="1.0" encoding="utf-8"?>
<!--- KmcModule is an abstract class that holds common functionalities of KMC modules. -->
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" creationComplete="creationCompleteHandler(event)">
	<mx:Metadata>
		[ResourceBundle("dummy")]
	</mx:Metadata> 

	<mx:Script>
		<![CDATA[
			import com.kaltura.KalturaClient;
			import com.kaltura.commands.uiConf.UiConfGet;
			import com.kaltura.events.KalturaEvent;
			import com.kaltura.kmc.events.NavigationEvent;
			import com.kaltura.vo.KalturaUiConf;
			
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.events.ResourceEvent;
			import mx.resources.ResourceManager;

			// =====================================================
			// events
			// =====================================================
			
			/**
			 * Dispatched when the module needs to navigate to another module.
			 * @eventType com.kaltura.kmc.events.NavigationEvent
			 */
			[Event(name="navigate",type="com.kaltura.kmc.events.NavigationEvent")]
			
			/**
			 * Dispatched when the module encountered some error that prevents it from functioning.
			 * @eventType com.kaltura.events.KalturaEvent
			 */
			[Event(name="error",type="com.kaltura.events.KalturaEvent")]
			
			/**
			 * Dispatched when user clicked a help link.
			 * The <code>data</code> parameter is the anchor on help page.
			 * @eventType com.kaltura.events.KalturaEvent
			 */
			[Event(name="help",type="com.kaltura.events.KalturaEvent")]
			
			// =====================================================
			// members
			// =====================================================
			
			/**
			 * @copy #kc
			 * */
			protected var _kc:KalturaClient;

			/**
			 * @copy #context
			 * */
			protected var _context:Context;
			
			/**
			 * currently showing locale code
			 * */
			protected var _localeCode:String; 
			
			/**
			 * @copy #uiconfId
			 * */
			protected var _uiconfId:String;
			
			/**
			 * configuration object
			 * */
			protected var _uiconf:KalturaUiConf; 
			
			
			// =====================================================
			// methods
			// =====================================================
			
			/**
			 * module initialization
			 * */
			protected function creationCompleteHandler(event:FlexEvent):void {
				if (_uiconfId) {
					loadUiconf(_uiconfId);
				}
			}
			
			
			/**
			 * load configuration info
			 * */
			protected function loadUiconf(uiconfId:String):void {
				//TODO backdoor for local uiconf
				// - figure out what flashvar name to use to match all modules
				var uiconf:UiConfGet = new UiConfGet(int (uiconfId));
				uiconf.addEventListener(KalturaEvent.COMPLETE, configurationLoadHandler);
				uiconf.addEventListener(KalturaEvent.FAILED, configurationLoadFailedHandler);
				_kc.post(uiconf);
			}
			
			
			/**
			 * use configuration info
			 * @param e		data from server
			 * */
			protected function configurationLoadHandler(e:KalturaEvent):void {
				_uiconf = e.data as KalturaUiConf;
				var confFile:XML = new XML(_uiconf.confFile);
				
				loadLocale(confFile.locale.path.toString(), confFile.locale.language.toString());
			}
			
			
			/**
			 * failed loading uiconf
			 * @param e		data from server
			 * */
			protected function configurationLoadFailedHandler(e:KalturaEvent):void {
				//TODO use locale value instead of given error
				// - do we know the resourceBundle name? put all errors in "errors" bundle on all locales 
				//TODO use const for event type instead of a hardcoded string
				dispatchEvent(new KalturaEvent("error", false, false, false, null, e.error));
			}

			
			/**
			 * Load locale data.
			 * @param localePath	path to the locale (.swf) file
			 * @param language		locale code (i.e. en_US)
			 * */
			protected function loadLocale(localePath:String, language:String):void {
				_localeCode = language;
				var eventDispatcher:IEventDispatcher = ResourceManager.getInstance().loadResourceModule(localePath);
				eventDispatcher.addEventListener(ResourceEvent.COMPLETE, localeLoadCompleteHandler);
			}
			
			
			/**
			 * Set use of loaded locale.
			 * This is also the place to update any values which are not  
			 * bound to resource manager values and have to be set manualy.
			 * */
			protected function localeLoadCompleteHandler(event:ResourceEvent):void {
				event.target.removeEventListener(ResourceEvent.COMPLETE, localeLoadCompleteHandler);
				ResourceManager.getInstance().localeChain = [_localeCode];
			}        


			
			/**
			 * Tell KMC to switch to another module.
			 * @param module	name of module to show, should match the values listed in
			 * 					<code>events.NavigationEvent</code> class.
			 * @param subtab	name of subtab of the module to show. If <code>subtab</code> is supplied and the 
			 * 					module has a subtab with the same name it should show the matching subtab. 
			 * 					Otherwise it is up to the module to decide which subtab to show.
			 * */
			protected function navigate(module:String, subtab:String = ""):void {
				this.dispatchEvent(new NavigationEvent(NavigationEvent.NAVIGATE, module, subtab));
			}

			
			/**
			 * Navigate to a subtab in the module.
			 * Each module should implement this method according to its inner structure.
			 * @param subtab	name (id) of the required subtab. 
			 * */
			public function showSubtab(subtab:String):void {
				throw new Error("showSubtab must be implemented");
			}

			// =====================================================
			// getters / setters
			// =====================================================

			/**
			 * KalturaClient for server API calls
			 * */
			public function get kc():KalturaClient {
				return _kc;
			}


			/**
			 * @private
			 * */
			public function set kc(value:KalturaClient):void {
				_kc = value;
			}


			/**
			 * Application context
			 * */
			public function get context():Context {
				return _context;
			}


			/**
			 * @private
			 * */
			public function set context(value:Context):void {
				_context = value;
			}
			

			/**
			 * Id of uiconf that the module has to load.
			 * Setting this value after the module's creation complete 
			 * event will trigger loading of the given uiconf.
			 * */
			public function get uiconfId():String {
				return _uiconfId;
			}

			
			/**
			 * @private
			 */
			public function set uiconfId(value:String):void
			{
				_uiconfId = value;
				loadUiconf(_uiconfId);
			}

			

		]]>
	</mx:Script>
	<mx:HBox verticalAlign="middle">
		<mx:Label text="{ResourceManager.getInstance().getString('dummy', 'name')}" />
		<mx:Label text="baaaa" />
	</mx:HBox>
</mx:Module>
